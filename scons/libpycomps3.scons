# vim: set ft=python:
import sys
import json
import os.path
import distutils.sysconfig as sysconfig

import json

Import("SCONS_ROOT", "color_env", "env",
       "libcomps", "libcomps_include_path", "libcomps_path",
       "INSTALL_LIB_PATH", "INSTALL_LIB64_PATH",
       "INSTALL_BIN_PATH", "INSTALL_SHARE_PATH",
       "INSTALL_INC_PATH")

FORM_PATH_TO_SRCS = os.path.join(SCONS_ROOT, "../libcomps/src/python/src/%s")
PATH_TO_SRCS = os.path.join(SCONS_ROOT, "../libcomps/src/python/src/")

FORM_PATH_TO_TGTS = "os_files/%s.os"
PATH_TO_TGTS = "os_files"

PYCOMPS_SRCS = """pycomps.c pycomps_sequence.c pycomps_envs.c
                   pycomps_categories.c pycomps_groups.c pycomps_gids.c
                   pycomps_utils.c pycomps_dict.c pycomps_mdict.c
                   pycomps_hash.c pycomps_exc.c pycomps_lbw.c
"""

local_env = env.Clone(CCFLAGS = '-O2 --pedantic -Wall --std=c99 -Wextra ' +\
                            '-Wno-missing-field-initializers ' +\
                            '-fno-strict-aliasing -g',
                      LIBS = [env["PYTHON3_LIB"], env.get("LIBS"), "comps"],
                      LIBPATH = [env["PYTHON3_LIBPATH"], libcomps_path],
                      CPPPATH = [env["PYTHON3_INCPATH"], env["CPPPATH"],
                                 libcomps_include_path])

local_env.VariantDir(PATH_TO_TGTS , PATH_TO_SRCS, duplicate=0)

pycomps_files_fullp = [FORM_PATH_TO_SRCS % x for x in PYCOMPS_SRCS.split() if x]
pycomps_files_targets = []
for x in PYCOMPS_SRCS.split():
    if x:
        pycomps_files_targets.append(FORM_PATH_TO_TGTS % x.split(".")[0])

pycopy3 = env.CopyAs(os.path.join("_libpycomps", "__init__.py"),
                      os.path.join(SCONS_ROOT, "__init__.py"))

for t,s in zip(pycomps_files_targets, pycomps_files_fullp):
    so = local_env.SharedObject(t, s)

libpycomps3 = local_env.SharedLibrary(os.path.join("_libpycomps", "_libpycomps"),
                         pycomps_files_targets,
                        SHLIBPREFIX="")

libpycomps3_install = [local_env.Install(os.path.join(INSTALL_LIB64_PATH,
                                            "python-" + env["PYTHON3_VERSION"],
                                                     "site_packages",
                                                     "libcomps"),
                                         libpycomps3),
                       local_env.Install(os.path.join(INSTALL_LIB64_PATH,
                                            "python-" + env["PYTHON3_VERSION"],
                                                     "site_packages",
                                                     "libcomps"),
                                         os.path.join("_libpycomps",
                                                      "__init__.py"))]

local_env.Depends(libpycomps3_install, libpycomps3)
local_env.Export("libpycomps3_install")

local_env.Depends(libpycomps3, libcomps)
local_env.Depends(libpycomps3, pycopy3)
local_env.Export("libpycomps3")
